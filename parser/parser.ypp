%{
#include <iostream>
#include <string>
#include <map>
#include <vector>
#include <cstdlib> //-- I need this for atoi

#include "SemValue.h"
#include "Tree.h" // abstract syntax tree

//-- Lexer prototype required by bison, aka getNextToken()
int yylex();
int yyerror(const char *p) { std::cerr << "Error!" << std::endl; }
%}

//-- SYMBOL SEMANTIC VALUES -----------------------------
%token <sym> DATABASE DATABASES TABLE TABLES SHOW CREATE
%token <sym> DROP USE CHECK PRIMARY KEY UNIQUE
%token <sym> NOT NIL AUTO_INCREMENT INSERT INTO VALUES
%token <sym> DELETE FROM WHERE UPDATE SET SELECT
%token <sym> GROUP BY IS IN BETWEEN LIKE
%token <sym> AND OR SUM AVG MAX MIN
%token <sym> INT SMALLINT BIGINT FLOAT REAL DOUBLE
%token <sym> VARCHAR STRING CHAR BOOLEAN DATETIME
%token <sym> EQ NE LE GE LT GT
%token <sym> LP RP COMMA STOP
%token <sym> ALL DOT
%token <value> VALUE
%token <ident> IDENT

%type <tree> program stmt sysStmt dbStmt tbStmt type expr check primaryKey columns eq selector where groupBy selectors
%type <tlist> fields stmts set eqs sellist
%type <integer> attr
%type <ilist> attrs
%type <slist> idents

//-- GRAMMAR RULES ---------------------------------------
%%
program     :   stmts
                {
                    $$ = new TopLevel($1);
                    PrintWriter pw;
                    $$->printTo(pw);
                }

stmts       :   /* empty */
                {
                    $$ = new std::vector<Tree*>;
                }
            |   stmts stmt
                {
                    $$ = $1;
                    $$->push_back($2);
                }
            ;

stmt        :   sysStmt STOP
            |   dbStmt STOP
            |   tbStmt STOP
            ;

sysStmt     :   SHOW DATABASES
                {
                    $$ = new ListDB;
                }
            ;

dbStmt      :   CREATE DATABASE IDENT
                {
                    $$ = new CreateDBStmt($3);
                }
            |   DROP DATABASE IDENT
                {
                    $$ = new DropDBStmt($3);
                }
            |   USE IDENT
                {
                    $$ = new UseDBStmt($2);
                }
            |   SHOW TABLES
                {
                    $$ = new ListTB;
                }
            ;

tbStmt      :   CREATE TABLE IDENT LP fields check primaryKey RP
                {
                    $$ = new CreateTBStmt($3, $5, $6, $7);
                }
            |   DROP TABLE IDENT
                {
                    $$ = new DropTBStmt($3);
                }
            |   SHOW TABLE IDENT
                {
                    $$ = new ShowTBStmt($3);
                }
            |   INSERT INTO IDENT columns VALUES LP set RP
                {
                    $$ = new InsertStmt($3, $4, $7);
                }
            |   DELETE FROM IDENT WHERE expr
                {
                    $$ = new DeleteStmt($3, $5);
                }
            |   UPDATE IDENT SET eqs WHERE expr
                {
                    $$ = new UpdateStmt($2, $4, $6);
                }
            |   SELECT selectors FROM IDENT where groupBy
                {
                    $$ = new SelectStmt($4, $2, $5, $6);
                }
            ;

fields      :   IDENT type attrs
                {
                    $$ = new std::vector<Tree*>;
                    $$->push_back(new Field($2, $1, $3));
                }
            |   fields COMMA IDENT type attrs
                {
                    $$ = $1;
                    $$->push_back(new Field($4, $3, $5));
                }
            ;

type        :   INT
                {
                    $$ = new Type(Type::TYPE_INT);
                }
            |   INT LP VALUE RP
                {
                    $$ = new Type(Type::TYPE_INT, $3);
                }
            |   SMALLINT
                {
                    $$ = new Type(Type::TYPE_SMALL_INT);
                }
            |   BIGINT
                {
                    $$ = new Type(Type::TYPE_BIG_INT);
                }
            |   FLOAT
                {
                    $$ = new Type(Type::TYPE_FLOAT);
                }
            |   DOUBLE
                {
                    $$ = new Type(Type::TYPE_DOUBLE);
                }
            |   VARCHAR LP VALUE RP
                {
                    $$ = new Type(Type::TYPE_STRING, $3);
                }
            |   STRING
                {
                    $$ = new Type(Type::TYPE_STRING);
                }
            |   CHAR
                {
                    $$ = new Type(Type::TYPE_CHAR);
                }
            |   BOOLEAN
                {
                    $$ = new Type(Type::TYPE_BOOLEAN);
                }
            |   DATETIME
                {
                    $$ = new Type(Type::TYPE_DATETIME);
                }
            ;

attrs       :   /* empty */
                {
                    $$ = new std::vector<int>;
                }
            |   attrs attr
                {
                    $$ = $1;
                    $$->push_back($2);
                }
            ;

attr        :   NOT NIL
                {
                    $$ = Field::ATTR_NOT_NULL;
                }
            |   UNIQUE
                {
                    $$ = Field::ATTR_UNIQUE;
                }
            |   AUTO_INCREMENT
                {
                    $$ = Field::ATTR_AUTO_INCREMENT;
                }
            ;

check       :   /* empty */
                {
                    $$ = new Check;
                }
            |   COMMA CHECK LP expr RP
                {
                    $$ = new Check($4);
                }
            ;

primaryKey  :   /* empty */
                {
                    $$ = new PrimaryKey;
                }
            |   COMMA PRIMARY KEY LP IDENT RP
                {
                    $$ = new PrimaryKey($5);
                }
            ;

expr        :   IDENT IS NIL
                {
                    $$ = new UnonExpr($1, Expr::OP_IS_NULL);
                }
            |   IDENT IS NOT NIL
                {
                    $$ = new UnonExpr($1, Expr::OP_IS_NOT_NULL);
                }
            |   IDENT IN LP set RP
                {
                    $$ = new InExpr($1, $4, Expr::OP_IN);
                }
            |   IDENT NOT IN LP set RP
                {
                    $$ = new InExpr($1, $5, Expr::OP_NOT_IN);
                }
            |   IDENT BETWEEN VALUE AND VALUE
                {
                    $$ = new BetweenExpr($1, $3, $5, Expr::OP_BETWEEN);
                }
            |   IDENT NOT BETWEEN VALUE AND VALUE
                {
                    $$ = new BetweenExpr($1, $4, $6, Expr::OP_NOT_BETWEEN);
                }
            |   IDENT LIKE VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_LIKE);
                }
            |   IDENT NOT LIKE VALUE
                {
                    $$ = new BinExpr($1, $4, Expr::OP_NOT_LIKE);
                }
            |   IDENT EQ VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_EQ);
                }
            |   IDENT NE VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_NE);
                }
            |   IDENT GE VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_GE);
                }
            |   IDENT LE VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_LE);
                }
            |   IDENT GT VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_GT);
                }
            |   IDENT LT VALUE
                {
                    $$ = new BinExpr($1, $3, Expr::OP_LT);
                }
            |   LP expr RP
                {
                    $$ = $2;
                }
            |   expr AND expr
                {
                    $$ = new ComExpr($1, $3, Expr::OP_AND);
                }
            |   expr OR expr
                {
                    $$ = new ComExpr($1, $3, Expr::OP_OR);
                }
            ;

set         :   VALUE
                {
                    $$ = new std::vector<Tree*>;
                    $$->push_back($1);
                }
            |   set COMMA VALUE
                {
                    $$ = $1;
                    $$->push_back($3);
                }
            ;

columns     :   /* empty */
                {
                    $$ = new Columns;
                }
            |   LP idents RP
                {
                    $$ = new Columns($2);
                }
            ;

idents      :   IDENT
                {
                    $$ = new std::vector<const char*>;
                    $$->push_back($1);
                }
            |   idents COMMA IDENT
                {
                    $$ = $1;
                    $$->push_back($3);
                }
            ;

eqs         :   eq
                {
                    $$ = new std::vector<Tree*>;
                    $$->push_back($1);
                }
            |   eqs COMMA eq
                {
                    $$ = $1;
                    $$->push_back($3);
                }
            ;

eq          :   IDENT EQ VALUE
                {
                    $$ = new Eq($1, $3);
                }
            ;

selectors   :   ALL
                {
                    $$ = new Selectors;
                }
            |   sellist
                {
                    $$ = new Selectors($1);
                }
            ;

sellist     :   selector
                {
                    $$ = new std::vector<Tree*>;
                    $$->push_back($1);
                }
            |   sellist COMMA selector
                {
                    $$ = $1;
                    $$->push_back($3);
                }
            ;

selector    :   IDENT DOT IDENT
                {
                    $$ = new Selector($1, $3);
                }
            |   IDENT
                {
                    $$ = new Selector(Selector::FUNC_NULL, $1);
                }
            |   SUM LP IDENT RP
                {
                    $$ = new Selector(Selector::FUNC_SUM, $3);
                }
            |   AVG LP IDENT RP
                {
                    $$ = new Selector(Selector::FUNC_AVG, $3);
                }
            |   MAX LP IDENT RP
                {
                    $$ = new Selector(Selector::FUNC_MAX, $3);
                }
            |   MIN LP IDENT RP
                {
                    $$ = new Selector(Selector::FUNC_MIN, $3);
                }
            ;

where       :   /* empty */
                {
                    $$ = new Where;
                }
            |   WHERE expr
                {
                    $$ = new Where($2);
                }
            ;

groupBy     :   /* empty */
                {
                    $$ = new GroupBy;
                }
            |   GROUP BY IDENT
                {
                    $$ = new GroupBy($3);
                }
            ;

%%
//-- FUNCTION DEFINITIONS ---------------------------------
int main()
{
    yyparse();
    printf("exit!\n");

    return 0;
}
